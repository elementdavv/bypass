text := import("text")
brook := import("brook")
json := import("json")

dnsquery_handler := func(in){
    if in.domain == "example.com"{
        return {"block": true}
    }
    if in.domain == "example1.com"{
        return {"bypass": true}
    }
    if in.domain == "example2.com" {
        if in.type == "A" {
            return {ip: "10.211.1.76"}
        }
        if in.type == "AAAA" {
            return {"block": true}
        }
    }
}

address_handler := func(in) {
    if in.ipaddress {
        r := brook.splithostport(in.ipaddress)
        if r.host == "1.2.4.8" {
            return { "block": true }
        }
        s := brook.country(r.host)
        if s == "ZZ" || s == "CN" {
            return { "bypass": true }
        }
        return
    }
    if in.network == "tcp" && in.domainaddress {
        if in.domainaddress == "httpbin.org:80" {
            return {"mitm": true, "mitmprotocol": "http"}
        }
        if in.domainaddress == "httpbin.org:443" {
            return {"mitm": true, "mitmprotocol": "https", "mitmwithbody": true}
        }
        if in.domainaddress == "example3.com:80" {
            return {"bypass": true, "ipaddress": "10.211.1.76:8080", "mitm": true, "mitmprotocol": "http", "mitmwithbody": true}
        }
        if in.domainaddress == "myip.ipip.net:443" {
            return {"bypass": true, "ipaddressfrombypassdns": "A", "mitm": true, "mitmprotocol": "https", "mitmwithbody": true}
        }
        return
    }
    if in.network == "udp" && in.domainaddress {
        if text.has_suffix(in.domainaddress, "httpbin.org:443") {
            return { "block": true }
        }
        return
    }
}

httprequest_handler := func(request){
    if text.has_prefix(request["URL"], "http://httpbin.org") {
        response := {
            "StatusCode": 301,
            "Location": text.replace(request["URL"], "http://", "https://", 1)
        }
        return response
    }
    if text.has_prefix(request["URL"], "https://httpbin.org") {
        request["User-Agent"] = "curl/7.79.1"
        return request
    }
    return request
}

httpresponse_handler := func(request, response){
    delete(response, "Alt-Svc") // Avoid upgrading to http3
    if text.has_prefix(request["URL"], "https://httpbin.org/get") {
        j := json.decode(response["Body"])
        j.origin = "m.a.r.s"
        response["Body"] = json.encode(j)
        return response
    }
    if text.has_prefix(request["URL"], "https://myip.ipip.net") {
        response["Body"] += bytes(text.re_replace(`\d+\.\d+\.\d+\.\d+`, string(response["Body"]), "1.2.3.4"))
        return response
    }
    return response
}

handler := func(){
    if in_dnsquery {
        return dnsquery_handler(in_dnsquery)
    }
    if in_address {
        return address_handler(in_address)
    }
    if in_httprequest && !in_httpresponse {
        return httprequest_handler(in_httprequest)
    }
    if in_httprequest && in_httpresponse {
        return httpresponse_handler(in_httprequest, in_httpresponse)
    }
}

out := handler()
